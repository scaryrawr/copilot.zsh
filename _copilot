#compdef copilot yopilot

# Zsh completion for GitHub Copilot CLI

_copilot__models_from_help() {
    local help line rest
    local -a out
    local capturing=0

    help="$(_call_program copilot-help copilot --help 2>/dev/null)"
    [[ -z $help ]] && return 0

    for line in "${(@f)help}"; do
        if (( !capturing )) && [[ $line == *"--model <model>"*"(choices:"* ]]; then
            capturing=1
        fi
        if (( capturing )); then
            rest="$line"
            while [[ $rest =~ '"([^"]+)"' ]]; do
                out+=("${match[1]}")
                rest=${rest#*\"${match[1]}\"}
            done
            [[ $line == *")"* ]] && break
        fi
    done

    (( ${#out} )) && print -l -- $out
}

_copilot__installed_plugins() {
    local output line name
    output="$(_call_program copilot-plugins copilot plugin list 2>/dev/null)"
    [[ -z $output ]] && return 0
    for line in "${(@f)output}"; do
        if [[ $line == *•* ]]; then
            name="${line#*• }"
            name="${name%% *}"
            [[ -n $name ]] && print -- "$name"
        fi
    done
}

_copilot__marketplaces() {
    local output line name desc
    output="$(_call_program copilot-marketplaces copilot plugin marketplace list 2>/dev/null)"
    [[ -z $output ]] && return 0
    for line in "${(@f)output}"; do
        if [[ $line == *•* ]]; then
            name="${line#*• }"
            desc="${name#* }"
            name="${name%% *}"
            # strip surrounding parens from description
            desc="${desc#\(}"
            desc="${desc%\)}"
            [[ -n $name ]] && print -- "${name}:${desc}"
        fi
    done
}

_copilot__browsable_plugins() {
    local marketplace="$1" output line name desc
    output="$(_call_program copilot-browse copilot plugin marketplace browse "$marketplace" 2>/dev/null)"
    [[ -z $output ]] && return 0
    for line in "${(@f)output}"; do
        if [[ $line == *•* ]]; then
            name="${line#*• }"
            # format: "name - description"
            desc="${name#* - }"
            name="${name%% - *}"
            name="${name%% *}"
            [[ -n $name ]] && print -- "${name}@${marketplace}:${desc}"
        fi
    done
}

_copilot__installable_plugins() {
    local -a marketplaces
    marketplaces=("${(@f)$(_copilot__marketplaces)}")
    local mp
    for mp in $marketplaces; do
        _copilot__browsable_plugins "${mp%%:*}"
    done
}

_copilot__complete_installed_plugins() {
    local -a plugins
    plugins=("${(@f)$(_copilot__installed_plugins)}")
    (( ${#plugins} )) && _describe -t plugins 'installed plugins' plugins
}

_copilot__complete_installable_plugins() {
    local -a plugins
    plugins=("${(@f)$(_copilot__installable_plugins)}")
    (( ${#plugins} )) && _describe -t plugins 'available plugins' plugins
}

_copilot__complete_marketplaces() {
    local -a mkts
    mkts=("${(@f)$(_copilot__marketplaces)}")
    (( ${#mkts} )) && _describe -t marketplaces 'registered marketplaces' mkts
}

_copilot() {
    local -a options commands help_topics log_levels models stream_modes

    options=(
        '--acp[Start as Agent Client Protocol server]'
        '*--add-dir[Add a directory to the allowed list for file access]:directory:_directories'
        '*--add-github-mcp-tool[Add a tool to enable for the GitHub MCP server]:tool'
        '*--add-github-mcp-toolset[Add a toolset to enable for the GitHub MCP server]:toolset'
        '*--additional-mcp-config[Additional MCP servers configuration]:config'
        '--agent[Specify a custom agent to use]:agent'
        '--allow-all[Enable all permissions (tools, paths, URLs)]'
        '--allow-all-paths[Disable file path verification and allow access to any path]'
        '--allow-all-tools[Allow all tools to run automatically without confirmation]'
        '--allow-all-urls[Allow access to all URLs without confirmation]'
        '*--allow-tool[Allow specific tools]:tools'
        '*--allow-url[Allow access to specific URLs or domains]:url'
        '*--available-tools[Only these tools will be available to the model]:tools'
        '--banner[Show the startup banner]'
        '--config-dir[Set the configuration directory]:directory:_directories'
        '--continue[Resume the most recent session]'
        '*--deny-tool[Deny specific tools]:tools'
        '*--deny-url[Deny access to specific URLs or domains]:url'
        '--disable-builtin-mcps[Disable all built-in MCP servers]'
        '*--disable-mcp-server[Disable a specific MCP server]:server'
        '--disable-parallel-tools-execution[Disable parallel execution of tools]'
        '--disallow-temp-dir[Prevent automatic access to the system temporary directory]'
        '--enable-all-github-mcp-tools[Enable all GitHub MCP server tools]'
        '*--excluded-tools[These tools will not be available to the model]:tools'
        '--experimental[Enable experimental features]'
        '(-h --help)'{-h,--help}'[Display help for command]'
        '(-i --interactive)'{-i,--interactive}'[Start interactive mode and automatically execute this prompt]:prompt'
        '--log-dir[Set log file directory]:directory:_directories'
        '--log-level[Set the log level]:level:->log_levels'
        '--model[Set the AI model to use]:model:->models'
        '--no-ask-user[Disable the ask_user tool (agent works autonomously)]'
        '--no-auto-update[Disable downloading CLI update automatically]'
        '--no-color[Disable all color output]'
        '--no-custom-instructions[Disable loading of custom instructions from AGENTS.md]'
        '--no-experimental[Disable experimental features]'
        '--plain-diff[Disable rich diff rendering]'
        '(-p --prompt)'{-p,--prompt}'[Execute a prompt in non-interactive mode]:prompt'
        '--resume[Resume from a previous session]::session_id'
        '(-s --silent)'{-s,--silent}'[Output only the agent response (no stats)]'
        '--screen-reader[Enable screen reader optimizations]'
        '--share[Share session to markdown file after completion]::path:_files'
        '--share-gist[Share session to a secret GitHub gist after completion]'
        '--stream[Enable or disable streaming mode]:mode:->stream_modes'
        '(-v --version)'{-v,--version}'[Show version information]'
        '--yolo[Enable all permissions (equivalent to --allow-all)]'
    )

    commands=(
        'help:Display help information'
        'init:Initialize Copilot instructions for this repository'
        'login:Authenticate with Copilot via OAuth device flow'
        'plugin:Manage plugins and plugin marketplaces'
        'update:Download the latest version'
        'version:Display version information and check for updates'
    )

    help_topics=(
        'config:Configuration Settings'
        'commands:Interactive Mode Commands'
        'environment:Environment Variables'
        'logging:Logging'
        'permissions:Permissions'
    )

    log_levels=(
        'none:No logging'
        'error:Error level'
        'warning:Warning level'
        'info:Info level'
        'debug:Debug level'
        'all:All levels'
        'default:Default level'
    )

    # Fallback list; prefer dynamic extraction from `copilot --help` at completion time.
    models=(
        'claude-sonnet-4.5:Claude Sonnet 4.5'
        'claude-haiku-4.5:Claude Haiku 4.5'
        'claude-opus-4.6:Claude Opus 4.6'
        'claude-opus-4.5:Claude Opus 4.5'
        'claude-sonnet-4:Claude Sonnet 4'
        'gemini-3-pro-preview:Gemini 3 Pro Preview'
        'gpt-5.2-codex:GPT-5.2 Codex'
        'gpt-5.2:GPT-5.2'
        'gpt-5.1-codex-max:GPT-5.1 Codex Max'
        'gpt-5.1-codex:GPT-5.1 Codex'
        'gpt-5.1:GPT-5.1'
        'gpt-5:GPT-5'
        'gpt-5.1-codex-mini:GPT-5.1 Codex Mini'
        'gpt-5-mini:GPT-5 Mini'
        'gpt-4.1:GPT-4.1'
    )

    stream_modes=(
        'on:Enable streaming'
        'off:Disable streaming'
    )

    local -a plugin_commands plugin_marketplace_commands login_options plugin_options

    plugin_commands=(
        'install:Install a plugin'
        'uninstall:Uninstall a plugin'
        'update:Update a plugin to the latest version'
        'list:List installed plugins'
        'marketplace:Manage plugin marketplaces'
    )

    plugin_marketplace_commands=(
        'add:Add a marketplace'
        'remove:Remove a marketplace'
        'list:List registered marketplaces'
        'browse:Browse plugins in a marketplace'
    )

    login_options=(
        '--host[GitHub host URL]:host'
        '--config-dir[Set the configuration directory]:directory:_directories'
        '(-h --help)'{-h,--help}'[Display help for command]'
    )

    plugin_options=(
        '--config-dir[Path to the configuration directory]:directory:_directories'
        '(-h --help)'{-h,--help}'[Display help for command]'
    )

    _arguments -C \
        $options \
        '1: :->cmds' \
        '*::arg:->args'

    case $state in
        cmds)
            _describe -t commands 'copilot commands' commands
            ;;
        args)
            case $words[1] in
                help)
                    _describe -t help-topics 'help topics' help_topics
                    ;;
                login)
                    _arguments $login_options
                    ;;
                plugin)
                    case $words[2] in
                        install)
                            _arguments $plugin_options ':source:_copilot__complete_installable_plugins'
                            ;;
                        uninstall)
                            _arguments $plugin_options ':name:_copilot__complete_installed_plugins'
                            ;;
                        update)
                            _arguments $plugin_options ':name:_copilot__complete_installed_plugins'
                            ;;
                        list)
                            _arguments $plugin_options
                            ;;
                        marketplace)
                            case $words[3] in
                                add)
                                    _arguments $plugin_options ':source:'
                                    ;;
                                remove)
                                    _arguments \
                                        '(-f --force)'{-f,--force}'[Force removal even if plugins are installed]' \
                                        $plugin_options \
                                        ':name:_copilot__complete_marketplaces'
                                    ;;
                                list)
                                    _arguments $plugin_options
                                    ;;
                                browse)
                                    _arguments $plugin_options ':name:_copilot__complete_marketplaces'
                                    ;;
                                *)
                                    _describe -t plugin-marketplace-commands 'plugin marketplace commands' plugin_marketplace_commands
                                    ;;
                            esac
                            ;;
                        *)
                            _describe -t plugin-commands 'plugin commands' plugin_commands
                            ;;
                    esac
                    ;;
            esac
            ;;
        log_levels)
            _describe -t log-levels 'log levels' log_levels
            ;;
        models)
            local -a dyn_models
            dyn_models=("${(@f)$(_copilot__models_from_help)}")
            if (( ${#dyn_models} )); then
                _describe -t models 'AI models' dyn_models
            else
                _describe -t models 'AI models' models
            fi
            ;;
        stream_modes)
            _describe -t stream-modes 'streaming modes' stream_modes
            ;;
    esac
}

# Only execute in completion context.
if (( $+compstate )); then
    _copilot "$@"
fi
