#compdef copilot yopilot

# Zsh completion for GitHub Copilot CLI

_copilot__models_from_help() {
    local help line rest
    local -a out
    local capturing=0

    help="$(_call_program copilot-help copilot --help 2>/dev/null)"
    [[ -z $help ]] && return 0

    for line in "${(@f)help}"; do
        if (( !capturing )) && [[ $line == *"--model <model>"*"(choices:"* ]]; then
            capturing=1
        fi
        if (( capturing )); then
            rest="$line"
            while [[ $rest =~ '"([^"]+)"' ]]; do
                out+=("${match[1]}")
                rest=${rest#*\"${match[1]}\"}
            done
            [[ $line == *")"* ]] && break
        fi
    done

    (( ${#out} )) && print -l -- $out
}

_copilot() {
    local -a options commands help_topics log_levels models stream_modes

    options=(
        '--add-dir[Add a directory to the allowed list for file access]:directory:_directories'
        '--additional-mcp-config[Additional MCP servers configuration]:config'
        '--agent[Specify a custom agent to use]:agent'
        '--allow-all-paths[Disable file path verification and allow access to any path]'
        '--allow-all-tools[Allow all tools to run automatically without confirmation]'
        '--allow-tool[Allow specific tools]:tools'
        '--banner[Show the startup banner]'
        '--continue[Resume the most recent session]'
        '--deny-tool[Deny specific tools]:tools'
        '--disable-builtin-mcps[Disable all built-in MCP servers]'
        '--disable-mcp-server[Disable a specific MCP server]:server'
        '--disable-parallel-tools-execution[Disable parallel execution of tools]'
        '--disallow-temp-dir[Prevent automatic access to the system temporary directory]'
        '--enable-all-github-mcp-tools[Enable all GitHub MCP server tools]'
        '(-h --help)'{-h,--help}'[Display help for command]'
        '(-i --interactive)'{-i,--interactive}'[Start interactive mode and automatically execute this prompt]:prompt'
        '--log-dir[Set log file directory]:directory:_directories'
        '--log-level[Set the log level]:level:->log_levels'
        '--model[Set the AI model to use]:model:->models'
        '--no-color[Disable all color output]'
        '--no-custom-instructions[Disable loading of custom instructions from AGENTS.md]'
        '(-p --prompt)'{-p,--prompt}'[Execute a prompt directly without interactive mode]:prompt'
        '--resume[Resume from a previous session]:session_id'
        '--screen-reader[Enable screen reader optimizations]'
        '--stream[Enable or disable streaming mode]:mode:->stream_modes'
        '(-v --version)'{-v,--version}'[Show version information]'
    )

    commands=(
        'help:Display help information'
    )

    help_topics=(
        'config:Configuration Settings'
        'commands:Interactive Mode Commands'
        'environment:Environment Variables'
        'logging:Logging'
        'permissions:Tool Permissions'
    )

    log_levels=(
        'none:No logging'
        'error:Error level'
        'warning:Warning level'
        'info:Info level'
        'debug:Debug level'
        'all:All levels'
        'default:Default level'
    )

    # Fallback list; prefer dynamic extraction from `copilot --help` at completion time.
    models=(
        'claude-opus-4.5:Claude Opus 4.5'
        'claude-sonnet-4.5:Claude Sonnet 4.5'
        'claude-sonnet-4:Claude Sonnet 4'
        'claude-haiku-4.5:Claude Haiku 4.5'
        'gpt-5:GPT-5'
        'gpt-5.1:GPT-5.1'
        'gpt-5.1-codex-mini:GPT-5.1 Codex Mini'
        'gpt-5.1-codex-max:GPT-5.1 Codex Max'
        'gpt-5.1-codex:GPT-5.1 Codex'
        'gpt-5-mini:GPT-5 Mini'
        'gpt-4.1:GPT-4.1'
        'gemini-3-pro-preview:Gemini 3 Pro Preview'
    )

    stream_modes=(
        'on:Enable streaming'
        'off:Disable streaming'
    )

    _arguments -C \
        $options \
        '1: :->cmds' \
        '*::arg:->args'

    case $state in
        cmds)
            _describe -t commands 'copilot commands' commands
            ;;
        args)
            case $words[1] in
                help)
                    _describe -t help-topics 'help topics' help_topics
                    ;;
            esac
            ;;
        log_levels)
            _describe -t log-levels 'log levels' log_levels
            ;;
        models)
            local -a dyn_models
            dyn_models=("${(@f)$(_copilot__models_from_help)}")
            if (( ${#dyn_models} )); then
                _describe -t models 'AI models' dyn_models
            else
                _describe -t models 'AI models' models
            fi
            ;;
        stream_modes)
            _describe -t stream-modes 'streaming modes' stream_modes
            ;;
    esac
}

# Only execute in completion context.
if (( $+compstate )); then
    _copilot "$@"
fi
